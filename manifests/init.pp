# Installs, configures, and manages nomad
#
# @example To set up a single nomad server, with several agents attached, on the server.
#   class { 'nomad':
#     config_hash => {
#       'region'     => 'us-west',
#       'datacenter' => 'ptk',
#       'log_level'  => 'INFO',
#       'bind_addr'  => '0.0.0.0',
#       'data_dir'   => '/opt/nomad',
#       'server'     => {
#         'enabled'          => true,
#         'bootstrap_expect' => 3,
#       }
#     }
#   }
#
# @example On the agent(s)
#   class { 'nomad':
#     config_hash => {
#       'region'     => 'us-west',
#       'datacenter' => 'ptk',
#       'log_level'  => 'INFO',
#       'bind_addr'  => '0.0.0.0',
#       'data_dir'   => '/opt/nomad',
#       'client'     => {
#         'enabled' => true,
#         'servers' => [
#           "nomad01.your-org.pvt:4647",
#           "nomad02.your-org.pvt:4647",
#           "nomad03.your-org.pvt:4647"
#         ]
#       }
#     },
#   }
#
# @example Install from zip file for a CPU architecture HashiCorp does not provide native packages for.
#   class { 'nomad':
#     arch                => 'armv7l',
#     install_method      => 'url',
#     manage_service_file => true,
#     version             => '1.0.3', # check latest version at https://github.com/hashicorp/nomad/blob/master/CHANGELOG.md
#     config_hash         => {
#       'region'     => 'us-west',
#       'datacenter' => 'ptk',
#       'log_level'  => 'INFO',
#       'bind_addr'  => '0.0.0.0',
#       'data_dir'   => '/opt/nomad',
#       'client'     => {
#         'enabled' => true,
#         'servers' => [
#           "nomad01.your-org.pvt:4647",
#           "nomad02.your-org.pvt:4647",
#           "nomad03.your-org.pvt:4647"
#         ]
#       }
#     },
#   }
#
# @example Disable install and service components
#   class { 'nomad':
#     install_method => 'none',
#     manage_service => false,
#     config_hash    => {
#       region     => 'us-west',
#       datacenter => 'ptk',
#       log_level  => 'INFO',
#       bind_addr  => '0.0.0.0',
#       data_dir   => '/opt/nomad',
#       'client'     => {
#         'enabled' => true,
#         'servers' => [
#           "nomad01.your-org.pvt:4647",
#           "nomad02.your-org.pvt:4647",
#           "nomad03.your-org.pvt:4647"
#         ]
#       }
#     },
#   }
#
# @param arch
#   cpu architecture
# @param purge_config_dir
#   Purge config files no longer generated by Puppet
# @param data_dir_mode
#   Specify unix permissions for data dir directory managed by this module
# @param plugin_dir_mode
#   Specify unix permissions for plugin dir directory managed by this module
# @param join_wan
#   join nomad cluster over the WAN
# @param bin_dir
#   location of the nomad binary
# @param version
#   Specify version of nomad binary to download.
# @param install_method
#   install via system package, download and extract from a url.
# @param os
#   operation system to install for
# @param download_url
#   download url to download from
# @param download_url_base
#   download hostname to down from
# @param download_extension
#   archive type to download
# @param package_name
#   Only valid when the install_method == package.
# @param config_dir
#   location of the nomad configuration
# @param extra_options
#   Extra arguments to be passed to the nomad agent
# @param config_hash
#   Use this to populate the JSON config file for nomad.
# @param config_defaults
#   default set of config settings
# @param config_validator
#   Use this to set the JSON config file validation command. It defaults to nomad validator which is currenly missing some validation checks.
#   If ruby is available on the system you could use 'ruby_validator', or create your own script (ending with space and % symbol).
# @param config_mode
#   Use this to set the JSON config file mode for nomad.
# @param manage_repo
#   Configure the upstream HashiCorp repository. Only relevant when $nomad::install_method = 'package'.
# @param manage_service
#   manage the nomad service
# @param manage_service_file
#   create and manage the systemd service file
# @param pretty_config
#   Generates a human readable JSON config file.
# @param service_enable
#   enable the nomad service
# @param service_ensure
#   ensure the state of the nomad service
# @param restart_on_change
#   Determines whether to restart nomad agent on $config_hash changes. This will not affect reloads when service, check or watch configs change.
# @param env_vars
#   Hash of optional environment variables that should be passed to nomad
# @param user
#   User to run the Nomad binary as. Also used as owner of directories and config files managed by this module.
# @param group
#   Group to run the Nomad binary as. Also used as group of directories and config files managed by this module.
# @param server_recovery
#   Nomad server outage recovery configuration
# @param recovery_nomad_server_regex
#  Regex to match Nomad server hostnames within the same puppet environment. It requires PuppetDB and it's mutually exclusive with nomad_server_hash.
# @param recovery_nomad_server_hash
#  If you don't have the PuppetDB you can supply a Hash with server IPs and corresponding node-ids. It works without PuppetDB and it's mutually exclusive with nomad_server_regex.
# @param recovery_network_interface
#  NIC where Nomad server IP is configured
# @param recovery_rpc_port
#  Nomad server RPC port
class nomad (
  String[1] $arch,
  Boolean $purge_config_dir                      = true,
  Stdlib::Filemode $data_dir_mode                = '0755',
  Stdlib::Filemode $plugin_dir_mode              = '0755',
  Optional[String[1]] $join_wan                  = undef,
  Stdlib::Absolutepath $bin_dir                  = '/usr/bin',
  String[1] $version                             = 'installed',
  Enum['none', 'package', 'url'] $install_method = 'package',
  String[1] $os                                  = downcase($facts['kernel']),
  Optional[String[1]] $download_url              = undef,
  String[1] $download_url_base                   = 'https://releases.hashicorp.com/nomad/',
  String[1] $download_extension                  = 'zip',
  String[1] $package_name                        = 'nomad',
  Stdlib::Absolutepath $config_dir               = '/etc/nomad.d',
  Optional[String[1]] $extra_options             = undef,
  Hash $config_hash                              = {},
  Hash $config_defaults                          = {},
  Stdlib::Filemode $config_mode                  = '0660',
  Boolean $pretty_config                         = false,
  Boolean $service_enable                        = true,
  Stdlib::Ensure::Service $service_ensure        = 'running',
  Boolean $manage_repo                           = true,
  Boolean $manage_service                        = true,
  Boolean $manage_service_file                   = false,
  Boolean $restart_on_change                     = true,
  Hash[String[1], String] $env_vars              = {},
  String[1] $user                                = 'root',
  String[1] $group                               = 'root',
  Boolean $server_recovery                       = false,
  Optional[String] $recovery_nomad_server_regex  = undef,
  Optional[Hash] $recovery_nomad_server_hash     = undef,
  Optional[String] $recovery_network_interface   = undef,
  Stdlib::Port $recovery_rpc_port                = 4647,
  Variant[
    Enum['nomad_validator', 'ruby_validator'], Pattern[/\A.*\ %\z/]
  ] $config_validator                            = 'nomad_validator',
) {
  $real_download_url = pick($download_url, "${download_url_base}${version}/${package_name}_${version}_${os}_${arch}.${download_extension}")
  $config_hash_real = deep_merge($config_defaults, $config_hash)

  if $config_hash_real['data_dir'] {
    $data_dir = $config_hash_real['data_dir']
  } else {
    $data_dir = undef
  }

  if $config_hash_real['plugin_dir'] {
    $plugin_dir = $config_hash_real['plugin_dir']
  } else {
    $plugin_dir = undef
  }

  if ($config_hash_real['ports'] and $config_hash_real['ports']['rpc']) {
    $rpc_port = $config_hash_real['ports']['rpc']
  } else {
    $rpc_port = 8400
  }

  if ($config_hash_real['addresses'] and $config_hash_real['addresses']['rpc']) {
    $rpc_addr = $config_hash_real['addresses']['rpc']
  } elsif ($config_hash_real['client_addr']) {
    $rpc_addr = $config_hash_real['client_addr']
  } else {
    $rpc_addr = $facts['networking']['interfaces']['lo']['ip']
  }

  $notify_service = $restart_on_change ? {
    true    => Class['nomad::run_service'],
    default => undef,
  }

  class { 'nomad::install': }
  -> class { 'nomad::config':
    notify => $notify_service,
  }
  -> class { 'nomad::run_service': }
  -> class { 'nomad::reload_service': }

  contain nomad::install
  contain nomad::config
  contain nomad::run_service
  contain nomad::reload_service

  if ($server_recovery) {
    if ($recovery_nomad_server_regex) and ($recovery_nomad_server_hash) {
      fail('You can only use one of the parameters: recovery_nomad_server_regex or recovery_nomad_server_hash')
    }
    elsif !($recovery_nomad_server_regex) and !($recovery_nomad_server_hash) {
      fail('You must use one of the parameters: recovery_nomad_server_regex or recovery_nomad_server_hash')
    }
    if !($recovery_network_interface) and ($recovery_nomad_server_regex) {
      fail('You must specify the network_interface parameter when using recovery_nomad_server_regex')
    }
    class { 'nomad::server_recovery':
      nomad_server_regex => $recovery_nomad_server_regex,
      nomad_server_hash  => $recovery_nomad_server_hash,
      rpc_port           => $recovery_rpc_port,
      network_interface  => $recovery_network_interface,
    }
  } else {
    file { '/usr/local/bin/nomad-server-outage-recovery.sh':
      ensure => 'absent',
    }
  }
}
